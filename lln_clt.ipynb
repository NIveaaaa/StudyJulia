{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Notebook created: 2018-05-23 03:37:55  \n",
    "Generated from: _build_jl/jl/lln_clt.rst  "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#=\n",
    "\n",
    "@author : Spencer Lyon <spencer.lyon@nyu.edu>\n",
    "          Victoria Gregory <victoria.gregory@nyu.edu>\n",
    "\n",
    "=#\n",
    "using Plots\n",
    "pyplot()\n",
    "using Distributions\n",
    "using LaTeXStrings\n",
    "\n",
    "n = 100\n",
    "srand(42)  # reproducible results\n",
    "\n",
    "# == Arbitrary collection of distributions == #\n",
    "distributions = Dict(\"student's t with 10 degrees of freedom\" => TDist(10),\n",
    "                 \"β(2, 2)\" => Beta(2.0, 2.0),\n",
    "                 \"lognormal LN(0, 1/2)\" => LogNormal(0.5),\n",
    "                 \"γ(5, 1/2)\" => Gamma(5.0, 2.0),\n",
    "                 \"poisson(4)\" => Poisson(4),\n",
    "                 \"exponential with lambda = 1\" => Exponential(1))\n",
    "\n",
    "num_plots = 3\n",
    "dist_data = zeros(num_plots, n)\n",
    "sample_means = []\n",
    "dist_means = []\n",
    "titles = []\n",
    "for i = 1:num_plots\n",
    "    dist_names = collect(keys(distributions))\n",
    "    # == Choose a randomly selected distribution == #\n",
    "    name = dist_names[rand(1:length(dist_names))]\n",
    "    dist = pop!(distributions, name)\n",
    "\n",
    "    # == Generate n draws from the distribution == #\n",
    "    data = rand(dist, n)\n",
    "\n",
    "    # == Compute sample mean at each n == #\n",
    "    sample_mean = Array{Float64}(n)\n",
    "    for j=1:n\n",
    "        sample_mean[j] = mean(data[1:j])\n",
    "    end\n",
    "\n",
    "    m = mean(dist)\n",
    "\n",
    "    dist_data[i, :] = data'\n",
    "    push!(sample_means, sample_mean)\n",
    "    push!(dist_means, m * ones(n))\n",
    "    push!(titles, name)\n",
    "\n",
    "end\n",
    "\n",
    "# == Plot == #\n",
    "N = repmat(reshape(repmat(1:n, 1, num_plots)', 1, n * num_plots), 2, 1)\n",
    "heights = [zeros(1, n * num_plots); reshape(dist_data, 1, n * num_plots)]\n",
    "plot(N, heights, layout=(3, 1), label=\"\", color=:grey, alpha=0.5)\n",
    "plot!(1:n, dist_data', layout=(3, 1), color=:grey, markershape=:circle,\n",
    "        alpha=0.5, label=\"\", linewidth=0)\n",
    "plot!(1:n, sample_means, linewidth=3, alpha=0.6, color=:green, legend=:topleft,\n",
    "      layout=(3, 1), label=[LaTeXString(\"\\$\\\\bar{X}_n\\$\") \"\" \"\"])\n",
    "plot!(1:n, dist_means, color=:black, linewidth=1.5, layout=(3, 1),\n",
    "      linestyle=:dash, grid=false, label=[LaTeXString(\"\\$\\\\mu\\$\") \"\" \"\"])\n",
    "plot!(title=reshape(titles, 1, length(titles)))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "srand(12)  # reproducible results\n",
    "n = 200\n",
    "dist = Cauchy()\n",
    "data = rand(dist, n)\n",
    "\n",
    "function plot_draws()\n",
    "    t = \"$n observations from the Cauchy distribution\"\n",
    "    N = repmat(linspace(1, n, n), 1, 2)'\n",
    "    heights = [zeros(1,n); data']\n",
    "    plot(1:n, data, color=:blue, markershape=:circle,\n",
    "         alpha=0.5, title=t, legend=:none, linewidth=0)\n",
    "    plot!(N, heights, linewidth=0.5, color=:blue)\n",
    "end\n",
    "\n",
    "plot_draws()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function plot_means()\n",
    "    # == Compute sample mean at each n == #\n",
    "    sample_mean = Array{Float64}(n)\n",
    "    for i=1:n\n",
    "        sample_mean[i] = mean(data[1:i])\n",
    "    end\n",
    "\n",
    "    # == Plot == #\n",
    "    plot(1:n, sample_mean, color=:red,\n",
    "         alpha=0.6, label=L\"$\\bar{X}_n$\",\n",
    "         linewidth=3, legendfont=font(12))\n",
    "    plot!(1:n, zeros(n), color=:black,\n",
    "          linewidth=1, linestyle=:dash, label=\"\", grid=false)\n",
    "end\n",
    "\n",
    "plot_means()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "srand(42)  # reproducible results\n",
    "ns = [1, 2, 4, 8]\n",
    "dom = 0:9\n",
    "\n",
    "pdfs = []\n",
    "titles = []\n",
    "for n in ns\n",
    "    b = Binomial(n, 0.5)\n",
    "    push!(pdfs, pdf(b, dom))\n",
    "    t = LaTeXString(\"\\$n = $n\\$\")\n",
    "    push!(titles, t)\n",
    "end\n",
    "\n",
    "bar(dom, pdfs, layout=4, alpha=0.6, xlims=(-0.5, 8.5), ylims=(0, 0.55),\n",
    "    xticks=dom, yticks=[0.0, 0.2, 0.4], legend=:none, title=reshape(titles, 1, length(titles)))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# == Set parameters == #\n",
    "srand(42)  # reproducible results\n",
    "n = 250    # Choice of n\n",
    "k = 10000  # Number of draws of Y_n\n",
    "dist = Exponential(1./2.)  # Exponential distribution, lambda = 1/2\n",
    "μ, s = mean(dist), std(dist)\n",
    "\n",
    "# == Draw underlying RVs. Each row contains a draw of X_1,..,X_n == #\n",
    "data = rand(dist, k, n)\n",
    "\n",
    "# == Compute mean of each row, producing k draws of \\bar X_n == #\n",
    "sample_means = mean(data, 2)\n",
    "\n",
    "# == Generate observations of Y_n == #\n",
    "Y = sqrt(n) * (sample_means .- μ)\n",
    "\n",
    "# == Plot == #\n",
    "xmin, xmax = -3 * s, 3 * s\n",
    "histogram(Y, nbins=60, alpha=0.5, xlims=(xmin, xmax),\n",
    "          norm=true, label=\"\")\n",
    "xgrid = linspace(xmin, xmax, 200)\n",
    "plot!(xgrid, pdf.(Normal(0.0, s), xgrid), color=:black,\n",
    "      linewidth=2, label=LaTeXString(\"\\$N(0, \\\\sigma^2=$(s^2))\\$\"),\n",
    "      legendfont=font(12))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using KernelDensity\n",
    "\n",
    "beta_dist = Beta(2.0, 2.0)\n",
    "\n",
    "\n",
    "function gen_x_draws(k)\n",
    "    bdraws = rand(beta_dist, 3, k)\n",
    "\n",
    "    # == Transform rows, so each represents a different distribution == #\n",
    "    bdraws[1, :] -= 0.5\n",
    "    bdraws[2, :] += 0.6\n",
    "    bdraws[3, :] -= 1.1\n",
    "\n",
    "    # == Set X[i] = bdraws[j, i], where j is a random draw from {1, 2, 3} == #\n",
    "    js = rand(1:3, k)\n",
    "    X = Array{Float64}(k)\n",
    "    for i=1:k\n",
    "        X[i]=  bdraws[js[i], i]\n",
    "    end\n",
    "\n",
    "    # == Rescale, so that the random variable is zero mean == #\n",
    "    m, sigma = mean(X), std(X)\n",
    "    return (X .- m) ./ sigma\n",
    "end\n",
    "\n",
    "nmax = 5\n",
    "reps = 100000\n",
    "ns = 1:nmax\n",
    "\n",
    "# == Form a matrix Z such that each column is reps independent draws of X == #\n",
    "Z = Array{Float64}(reps, nmax)\n",
    "for i=ns\n",
    "    Z[:, i] = gen_x_draws(reps)\n",
    "end\n",
    "\n",
    "# == Take cumulative sum across columns\n",
    "S = cumsum(Z, 2)\n",
    "\n",
    "# == Multiply j-th column by sqrt j == #\n",
    "Y = S .* (1. ./ sqrt.(ns))'\n",
    "\n",
    "# == Plot == #\n",
    "a, b = -3, 3\n",
    "gs = 100\n",
    "xs = linspace(a, b, gs)\n",
    "\n",
    "x_vec = []\n",
    "y_vec = []\n",
    "z_vec = []\n",
    "colors = []\n",
    "for n=ns\n",
    "    kde_est = kde(Y[:, n])\n",
    "    _xs, ys = kde_est.x, kde_est.density\n",
    "    push!(x_vec, collect(_xs))\n",
    "    push!(y_vec, ys)\n",
    "    push!(z_vec, collect(n*ones( length(_xs))))\n",
    "    push!(colors, RGBA(0, 0, 0, 1-(n-1)/nmax))\n",
    "end\n",
    "\n",
    "plot(x_vec, z_vec, y_vec, color = reshape(colors,1,length(colors)), legend=:none)\n",
    "plot!(xlims=(a,b), xticks=[-3; 0; 3], ylims=(1, nmax), yticks=ns, ylabel=\"n\",\n",
    "    xlabel = \"\\$ Y_n \\$\", zlabel = \"\\$ p(y_n) \\$\" , zlims=(0, 0.4), zticks=[0.2; 0.4])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# == Set parameters == #\n",
    "srand(42)   # reproducible results\n",
    "n = 250     # Choice of n\n",
    "k = 100000  # Number of draws of Y_n\n",
    "dist = Uniform(0, π/2)\n",
    "μ, s = mean(dist), std(dist)\n",
    "\n",
    "g = sin\n",
    "g′ = cos\n",
    "\n",
    "# == Draw underlying RVs. Each row contains a draw of X_1,..,X_n == #\n",
    "data = rand(dist, k, n)\n",
    "\n",
    "# == Compute mean of each row, producing k draws of \\bar X_n == #\n",
    "sample_means = mean(data, 2)\n",
    "\n",
    "error_obs = sqrt(n) .* (g.(sample_means) - g.(μ))\n",
    "\n",
    "# == Plot == #\n",
    "asymptotic_sd = g′(μ) .* s\n",
    "xmin = -3 * g′(μ) * s\n",
    "xmax = -xmin\n",
    "histogram(error_obs, nbins=60, alpha=0.5, normed=true, label=\"\")\n",
    "xgrid = linspace(xmin, xmax, 200)\n",
    "plot!(xgrid, pdf.(Normal(0.0, asymptotic_sd), xgrid), color=:black,\n",
    "      linewidth=2, label=LaTeXString(\"\\$N(0, g'(\\\\mu)^2\\\\sigma^2\\$)\"),\n",
    "      legendfont=font(12), xlims=(xmin, xmax), grid=false)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# == Set parameters == #\n",
    "n = 250\n",
    "replications = 50000\n",
    "dw = Uniform(-1, 1)\n",
    "du = Uniform(-2, 2)\n",
    "sw, su = std(dw), std(du)\n",
    "vw, vu = sw^2, su^2\n",
    "Σ = [vw    vw\n",
    "     vw vw+vu]\n",
    "\n",
    "# == Compute Σ^{-1/2} == #\n",
    "Q = inv(sqrtm(Σ))\n",
    "\n",
    "# == Generate observations of the normalized sample mean == #\n",
    "error_obs = Array{Float64}(2, replications)\n",
    "for i=1:replications\n",
    "    # == Generate one sequence of bivariate shocks == #\n",
    "    X = Array{Float64}(2, n)\n",
    "    W = rand(dw, n)\n",
    "    U = rand(du, n)\n",
    "\n",
    "    # == Construct the n observations of the random vector == #\n",
    "    X[1, :] = W\n",
    "    X[2, :] = W + U\n",
    "\n",
    "    # == Construct the i-th observation of Y_n == #\n",
    "    error_obs[:, i] = sqrt(n) .* mean(X, 2)\n",
    "end\n",
    "\n",
    "chisq_obs = squeeze(sum((Q * error_obs).^2, 1), 1)\n",
    "\n",
    "# == Plot == #\n",
    "xmin, xmax = 0, 8\n",
    "histogram(chisq_obs, nbins=50, normed=true, label=\"\")\n",
    "xgrid = linspace(xmin, xmax, 200)\n",
    "plot!(xgrid, pdf.(Chisq(2), xgrid), color=:black,\n",
    "      linewidth=2, label=\"Chi-squared with 2 degrees of freedom\",\n",
    "      legendfont=font(12), xlims=(xmin, xmax), grid=false)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.6.2",
   "language": "julia",
   "name": "julia-0.6"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}